#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/nBEEZ.png
#

# Load dependencies.
pmodload 'helper'

function prompt_sorin_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_sorin_pwd="$MATCH"
    unset MATCH
  else
    _prompt_sorin_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_sorin_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_sorin_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_sorin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_sorin_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
if [[ $TERM != xterm-256color && $TERM != terminology && $TERM != rxvt-unicode-256color && $TERM != screen-256color ]] ; then
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}>%F{yellow}>%F{green}>%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}I%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}<%F{yellow}<%F{red}<%f%b'
else
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'
fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}added%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}ahead%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}behind%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}deleted%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}modified%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}renamed%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}stashed%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}unmerged%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}untracked%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='${_prompt_sorin_username}%F{cyan}${_prompt_sorin_pwd}%f${git_info:+${(e)git_info[prompt]}}%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}%?%f)${VIM:+" %B%F{green}V%f%b"}${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}


# show username if is not our default username
if [[ -s /etc/elive-version ]] ; then
    # temporal variable to store the data
    _prompt_sorin_username="$( grep "^first-user: " /etc/elive-version | sed -e 's|^first-user: ||g' )"

    if [[ -n "$_prompt_sorin_username" ]] ; then
        # append username to prompt if we are not our own username
        if [[ "$_prompt_sorin_username" != "$USER" ]] ; then
            if [[ -n "$SSH_CLIENT" ]] || [[ "$TERM" = "screen-256color" ]] ; then
                if [[ "$USER" = "root" ]] ; then
                    # add a : so that you can hilight and copypaste more easly the full address
                    _prompt_sorin_username="%F{red}${USER}%F{white}@%F{green}$(hostname)%F{white}:"
                else
                    _prompt_sorin_username="%F{cyan}${USER}%F{white}@%F{green}$(hostname)%F{white}:"
                fi
            else
                if [[ "$USER" = "root" ]] ; then
                    # root is always red, important to see that add a space too, to show it better
                    _prompt_sorin_username="%F{red}$USER "
                else
                    # add a space too, to show it better
                    _prompt_sorin_username="%F{cyan}$USER "
                fi
            fi
        else
            unset _prompt_sorin_username
        fi
    else
        unset _prompt_sorin_username
    fi
fi


prompt_sorin_setup "$@"
